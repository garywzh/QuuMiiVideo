apply plugin: 'com.android.application'

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId 'org.garywzh.quumiibox'
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 5
        versionName '1.0.5'
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "umeng"]
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称为QuumiiBox_v1.0_2015-10-10_firim.apk
                        def fileName = "QuumiiBox_v${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '.debug'
        }
    }

    productFlavors {
        firim {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "firim"]
        }
        pre {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "pre"]
        }

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.squareup.okhttp:okhttp:2.3.0'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.google.code.gson:gson:2.4'
    compile 'org.jsoup:jsoup:1.8.2'
    compile 'com.github.bumptech.glide:okhttp-integration:1.3.1'
    compile 'com.google.guava:guava:18.0'
    compile 'com.umeng.analytics:analytics:latest.integration'
    compile 'com.android.support:support-v4:23.1.1'
}