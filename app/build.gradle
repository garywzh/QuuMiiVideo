apply plugin: 'com.android.application'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.3'

    defaultConfig {
        applicationId 'org.garywzh.quumiibox'
        minSdkVersion 17
        targetSdkVersion 23
        versionCode 12
        versionName '1.4.0'
        manifestPlaceholders = [UMENG_APPKEY_VALUE: properties.get('umengAppKey'), UMENG_CHANNEL_VALUE: "umeng"]
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        def fileName = "QuumiiBox_v${defaultConfig.versionName}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '.debug'
        }
    }

    productFlavors {
        play {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "play"]
        }
        firim {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "firim"]
        }
    }
}

ext {
    support_version = '23.4.0'
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile "com.android.support:appcompat-v7:$support_version"
    compile "com.android.support:support-v4:$support_version"
    compile "com.android.support:recyclerview-v7:$support_version"
    compile "com.android.support:design:$support_version"
    compile 'com.squareup.okhttp3:okhttp:3.3.1'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.google.guava:guava:19.0'
    compile 'com.umeng.analytics:analytics:latest.integration'
    compile 'com.google.android.exoplayer:exoplayer:r1.5.8'
    compile 'com.bignerdranch.android:recyclerview-multiselect:0.2'
}