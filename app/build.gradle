apply plugin: 'com.android.application'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.1'

    defaultConfig {
        applicationId 'org.garywzh.quumiibox'
        minSdkVersion 17
        targetSdkVersion 25
        versionCode 14
        versionName '1.5.1'
        manifestPlaceholders = [UMENG_APPKEY_VALUE: properties.get('umengAppKey'), UMENG_CHANNEL_VALUE: "umeng"]
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        def fileName = "QuumiiBox_v${defaultConfig.versionName}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '.debug'
        }
    }

    productFlavors {
        play {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "play"]
        }
        firim {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "firim"]
        }
    }
}

ext {
    support_version = '25.0.1'
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile "com.android.support:appcompat-v7:$support_version"
    compile "com.android.support:recyclerview-v7:$support_version"
    compile "com.android.support:design:$support_version"

    compile 'com.squareup.okhttp3:logging-interceptor:3.4.2'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'

    compile 'io.reactivex:rxjava:1.2.3'
    compile 'io.reactivex:rxandroid:1.2.1'

    compile 'com.github.bumptech.glide:glide:3.8.0-SNAPSHOT'
    compile 'com.google.guava:guava:19.0'
    compile 'com.umeng.analytics:analytics:latest.integration'
    compile 'com.google.android.exoplayer:exoplayer:r1.5.12'
    compile 'com.bignerdranch.android:recyclerview-multiselect:0.2'
}